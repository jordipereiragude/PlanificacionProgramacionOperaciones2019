{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using DataStructures # -> declaración de datos\n",
    "#some useful reminders --> https://learnxinyminutes.com/docs/julia/\n",
    "#otro useful reminder --> https://docs.julialang.org/en/v1/base/sort/"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Procesos\n",
    "\n",
    "Desde un punto de vista computacional, una de las características más significativas de los algoritmos de flujos es que nos obligan a estructurar las interacciones entre varios códigos para resolver el problema conjuntamente.\n",
    "\n",
    "Aquí veremos un caso, flujo máximo que necesitará el desarrollo de varios bloques que compondrán el conjunto final"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Funcionamiento de caminos mínimos"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "type grafoCaminos\n",
    "    vertices::Int64\n",
    "    longitud::Array{Int64,2} #longitud[i,j] indica la longitud -> también se puede usar para existencia de arco. Si un arco existe tiene un costo bajo, si no existe, tiene un costo altísimo\n",
    "end\n",
    "\n",
    "function generarGrafoCaminos(n::Int64,maxLongitud::Int64)\n",
    "  a=Array{Int64}(n,n) #matriz longitud\n",
    "  rand!(a,1:maxLongitud) #aquí se llena la matriz longitud con distancias aleatorias\n",
    "  return grafoCaminos(n,a)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Algoritmo de Djikstra\n",
    "#----------------------------------\n",
    "function djikstra(grafo::grafoCaminos,origen::Int64) #primero el grafo, segundo qué vértice es el origen\n",
    "    ∞=typemax(Int64) #el valor infinito\n",
    "    tr=zeros(Int64,grafo.vertices) # tr -> tr[i] es el último vértice visitado en el camino entre origen e i\n",
    "    l=Array{Int64}(grafo.vertices) # l -> l[i] es la distancia entre origen y el vértice i \n",
    "    fill!(l,∞) #todas las distancias son infinito (no se puede llegar de origen a ninguna parte)\n",
    "    l[origen]=0 #excepto el vértice origen\n",
    "    Q=PriorityQueue()\n",
    "    enqueue!(Q,origen,0) #guardar un vértice en la priority queue\n",
    "    while isempty(Q)==false #mientras la cola no esté vacía\n",
    "        v=dequeue!(Q) #voy a intentar mejorar los caminos desde v\n",
    "        for w in 1:grafo.vertices # mirar los arcos entre[v,w] donde w es cualquier vértice\n",
    "            if l[w] > (l[v]+grafo.longitud[v,w]) #si el camino actual l[w] es mayor al nuevo camino\n",
    "                l[w]=l[v]+grafo.longitud[v,w] #cambio longitud\n",
    "                tr[w]=v #y cambio último vértice\n",
    "                if haskey(Q,w) #guardo en cola de prioridad\n",
    "                    Q[w]=l[w]\n",
    "                else\n",
    "                    enqueue!(Q,w,l[w])\n",
    "                end #fin guardo en cola de prioridad\n",
    "            end\n",
    "            assert(l[w]>(0-1000000)) #si el valor del camino entre origen y w es muy pequeño, (negativo) va a haber un problema, para de calcular\n",
    "        end\n",
    "    end\n",
    "    return l,tr #devolver el vector de distancias y el vector que se usa para recontruir el camino\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Algoritmo de Bellman Ford\n",
    "#---------------------------\n",
    "function bellmanFord(g::grafoCaminos,origen::Int64)\n",
    "  # paso 1 (inicializamos)\n",
    "  ∞=typemax(Int64)\n",
    "  tr=zeros(Int64,g.vertices)\n",
    "  l=Array{Int64}(g.vertices)\n",
    "  fill!(l,∞)\n",
    "  l[origen]=0 #hasta aquí es como Djikstra\n",
    "  # paso 2 (recurrencia de programación dinámica)\n",
    "  for i = 1:g.vertices-1 #número de arcos (voy a buscar caminos con 1 arco, 2 arcos, 3 arcos...)\n",
    "    for v in 1:g.vertices\n",
    "      for w in 1:g.vertices #dos loops para pasar por todos los arcos -> camino hasta v + arco[v,w]\n",
    "        if l[w]> (l[v]+g.longitud[v,w]) #si el mejor camino conocido hasta ahora (con i-1 arcos) es peor que camino con i arcos + arco[v,w]\n",
    "          l[w]=l[v]+g.longitud[v,w] #hay que mejorar\n",
    "          tr[w]=v\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  # paso 3 (búsqueda de un circuito)\n",
    "  for v in 1:g.vertices\n",
    "    for w in 1:g.vertices #dos loops para pasar por todos los arcos\n",
    "      if l[w] > (l[v]+g.longitud[v,w])\n",
    "        #println(\"longitud: \",l[w],\" vs \",l[v]+g.longitud[v,w],\" y v: \",v,\" w: \",w)\n",
    "        visited=zeros(Bool,g.vertices)\n",
    "        while visited[w]==false\n",
    "          visited[w]=true\n",
    "          w=tr[w]\n",
    "        end\n",
    "        return w,l,tr #indicamos un vértice que forma parte del circuito, la longitud desde origen al resto y el vértice que me permitirá reconstruir el circuito\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  return 0,l,tr #salida normal, 0 porque no hay circuitos\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "unGrafo=generarGrafoCaminos(25,10000)\n",
    "unGrafo.longitud[13,14]=1; unGrafo.longitud[14,15]=1; unGrafo.longitud[15,16]=1; unGrafo.longitud[16,17]=(-10); \n",
    "unGrafo.longitud[17,13]=1; \n",
    "conCircuito,longitud,traza=bellmanFord(unGrafo,1)\n",
    "println(\"traza: \",traza)\n",
    "println(\"vertice conCircuito:\",conCircuito)\n",
    "# Si he encontrado un circuito, imprimirlo\n",
    "if conCircuito>0\n",
    "  println(\"arco: (\",traza[conCircuito],\",\",conCircuito,\")\")\n",
    "  w=traza[conCircuito]\n",
    "  while w != conCircuito\n",
    "    println(\"arco: (\",traza[w],\",\",w,\")\")\n",
    "    w=traza[w]\n",
    "  end\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "longitud,traza=djikstra(unGrafo,1) #si se descomenta salta la \"assertion\" de circuito negativo"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Funcionamiento flujo máximo"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "type grafoFlujos #estructura que necesito para problema de flujos\n",
    "    vertices::Int64 #los vértices\n",
    "    capacidad::Array{Int64,2} #capacidad[i,j] indica la capacidad del arco (la capacidad entre parejas de vértices)\n",
    "    solucion::Array{Int64,2}  #solucion[i,j] indica el flujo del arco (la solución)\n",
    "end\n",
    "\n",
    "function generarGrafoFlow(n::Int64,maxCapacidad::Int64,density::Float64) #rutina que genera grafos para flujos de forma aleatoria\n",
    "  a=Array{Int64}(n,n)\n",
    "  b=Array{Int64}(n,n)\n",
    "  rand!(a,1:maxCapacidad)\n",
    "  for i in 1:n\n",
    "    a[i,i]=0\n",
    "    for j in i+1:n\n",
    "      if rand()>(2*density) #números entre 0 y 1\n",
    "        a[i,j]=0\n",
    "        a[j,i]=0\n",
    "      else # aceptaremos flujo en un único sentido \n",
    "        if rand()<0.5\n",
    "          a[i,j]=0\n",
    "        else\n",
    "          a[j,i]=0\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  return grafoFlujos(n,a,b)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "slideshow": {
     "slide_type": "slide"
    }
   },
   "outputs": [],
   "source": [
    "# Implementación de Ford Fulkerson\n",
    "#---------------------------------\n",
    "function fordFulkerson(g::grafoFlujos,origen::Int64,destino::Int64)\n",
    "  fill!(g.solucion,0) #limpiar la solución\n",
    "  residual=grafoCaminos(g.vertices,zeros(Int64,g.vertices,g.vertices)) #grafo de soporte\n",
    "  obj=0\n",
    "  #println(\"g.capacidad. \",g.capacidad)\n",
    "  while true \n",
    "    #1. creo el grafo residual\n",
    "    fill!(residual.longitud,g.vertices+1)\n",
    "    for i in 1:g.vertices\n",
    "      for j in 1:g.vertices\n",
    "        if g.capacidad[i,j]>0 #si tiene capacidad  puede ser que haya arco en sentido directo e inverso\n",
    "          if g.solucion[i,j]<g.capacidad[i,j] #si no está saturado hay arco en sentido directo\n",
    "            residual.longitud[i,j]=1 #directo i,j\n",
    "          end\n",
    "          if g.solucion[i,j]>0 #si tiene flujo positivo hay arco en sentido inverso\n",
    "            residual.longitud[j,i]=1 #inverso j,i\n",
    "          end\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "    #2. resuelvo Dijkstra\n",
    "    #println(residual)\n",
    "    longitud,traza=djikstra(residual,origen)\n",
    "    println(\"longitud:\",longitud[destino])\n",
    "    if longitud[destino]<g.vertices #si he podido hacer un camino en el grafo residual entre origen y destino\n",
    "      extraFlow=typemax(Int32) \n",
    "      # primero tenemos que ver el flujo máximo que podemos pasar\n",
    "      c=destino\n",
    "      while true #busco el camino y determino su capacidad\n",
    "        #print(\"arco entre: \",traza[c],\" y \",c)\n",
    "        if g.capacidad[traza[c],c]>0\n",
    "          #estoy añadiendo flujo\n",
    "          extraFlow=min(extraFlow,g.capacidad[traza[c],c]-g.solucion[traza[c],c])\n",
    "          #println(\" capacidad \",g.capacidad[traza[c],c]-g.solucion[traza[c],c],\" extra: \",extraFlow)\n",
    "        else\n",
    "          #esto eliminando flujo (redirección)\n",
    "          extraFlow=min(extraFlow,g.solucion[c,traza[c]])\n",
    "          #println(\" backwards \",g.solucion[c,traza[c]],\" extra: \",extraFlow)\n",
    "        end\n",
    "        c=traza[c]\n",
    "        if c==origen\n",
    "          break\n",
    "        end\n",
    "      end\n",
    "      # segundo tenemos que actualizar el flujo de los arcos según flujo máximo\n",
    "      c=destino\n",
    "      while true\n",
    "        #print(\"cambio en arco \",traza[c],\",\",c)\n",
    "        if g.capacidad[traza[c],c]>0\n",
    "          g.solucion[traza[c],c] += extraFlow\n",
    "          #println(\" added \",extraFlow)\n",
    "          assert(g.solucion[traza[c],c]<=g.capacidad[traza[c],c])\n",
    "        else\n",
    "          g.solucion[c,traza[c]] -= extraFlow\n",
    "          #println(\" removed \",extraFlow)\n",
    "          assert(g.solucion[traza[c],c]>=0)\n",
    "        end\n",
    "        c=traza[c]\n",
    "        if c==origen\n",
    "          break\n",
    "        end\n",
    "      end\n",
    "      obj += extraFlow\n",
    "    else\n",
    "      #no podemos mejorar la solución\n",
    "      return obj\n",
    "    end\n",
    "    #println(\"solucion (un paso): \",g.solucion)\n",
    "  end\n",
    "  return totalFlow\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "gEjemplo=grafoFlujos(7,[0 1 3 0 0 0 0 ; 0 0 0 5 4 0 0 ; 0 0 0 3 0 0 0 ; 0 0 0 0 0 0 2 ; 0 0 0 0 0 2 0 ; 0 0 0 0 0 0 3 ; 0 0 0 0 0 0 0],zeros(Int64,7,7))\n",
    "maxFlow=fordFulkerson(gEjemplo,1,7)\n",
    "println(\"maxFlow: \",maxFlow)\n",
    "println(\"solucion: \",gEjemplo.solucion)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "gClase=grafoFlujos(8,[0 100 0 0 0 0 0 0; 0 0 60 30 0 0 0 0; 0 0 0 0 20 40 0 0; 0 0 0 0 0 30 0 0; 0 0 0 0 0 0 30 0; 0 0 0 0 0 0 60 0; 0 0 0 0 0 0 0 90; 0 0 0 0 0 0 0 0],zeros(Int64,8,8))\n",
    "flow=fordFulkerson(gClase,1,8)\n",
    "println(\"maxFlow: \",flow)\n",
    "println(\"solucion: \",gClase.solucion)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "gFlujos=generarGrafoFlow(10,500,0.45)\n",
    "maxFlow=fordFulkerson(gFlujos,1,10)\n",
    "println(\"maxFlow: \",maxFlow)\n",
    "println(\"solucion: \",gFlujos.solucion)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Funcionamiento flujo máximo a coste mínimo"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "type grafoMinCost\n",
    "    grafoF::grafoFlujos\n",
    "    coste::Array{Int64,2}  #coste[i,j] indica el coste de pasar una unidad de flujo por el arco\n",
    "end\n",
    "\n",
    "function generarGrafoMinCost(n::Int64,maxCapacidad::Int64,maxCost::Int64,density::Float64)\n",
    "  a=Array{Int64}(n,n)\n",
    "  b=Array{Int64}(n,n)\n",
    "  c=Array{Int64}(n,n)\n",
    "  rand!(a,1:maxCapacidad)\n",
    "  for i in 1:n\n",
    "    a[i,i]=0\n",
    "    for j in i+1:n\n",
    "      if rand()>(2*density) #números entre 0 y 1\n",
    "        a[i,j]=0\n",
    "        a[j,i]=0\n",
    "      else\n",
    "        if rand()<0.5\n",
    "          a[i,j]=0\n",
    "        else\n",
    "          a[j,i]=0\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  for i in 1:n\n",
    "    for j in 1:n\n",
    "      if a[i,j]>0\n",
    "        c[i,j]=rand(1:maxCost)\n",
    "      else\n",
    "        c[i,j]=0\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  return grafoMinCost(grafoFlujos(n,a,b),c)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Implementación\n",
    "#----------------------------------\n",
    "function minCostMaxFlow(g::grafoMinCost,origen::Int64,destino::Int64)\n",
    "  # primer paso. resolver maxflow\n",
    "  maxFlow=fordFulkerson(g.grafoF,origen,destino)\n",
    "  coste=0 # determinamos su coste\n",
    "  for i in 1:g.grafoF.vertices\n",
    "    for j in 1:g.grafoF.vertices #arcos entre [i,j]\n",
    "      if g.grafoF.solucion[i,j]>0 #si hay flujo\n",
    "        coste += g.grafoF.solucion[i,j]*g.coste[i,j] #sumo al coste\n",
    "      end\n",
    "    end\n",
    "  end\n",
    "  println(\"maxFlow: \",maxFlow,\"\\t\",\"coste inicial: \",coste)\n",
    "  # la idea ahora es la misma que e flujos máximos, pero usamos Bellman Ford para detectar un ciclo de coste negativo \n",
    "  # en el grafo\n",
    "  residual=grafoCaminos(g.grafoF.vertices+1,zeros(Int64,g.grafoF.vertices+1,g.grafoF.vertices+1))\n",
    "  while true #mientras mejora\n",
    "    #Primero creo un grafo residual\n",
    "    fill!(residual.longitud,typemax(Int32)) \n",
    "    for i in 1:g.grafoF.vertices\n",
    "      for j in 1:g.grafoF.vertices\n",
    "        if g.grafoF.capacidad[i,j]>0  \n",
    "          if g.grafoF.solucion[i,j]<g.grafoF.capacidad[i,j]\n",
    "            residual.longitud[i,j]=g.coste[i,j]\n",
    "          end\n",
    "          if g.grafoF.solucion[i,j]>0\n",
    "            residual.longitud[j,i]=0-g.coste[i,j]\n",
    "          end\n",
    "        end\n",
    "      end\n",
    "    end\n",
    "    for j in 1:g.grafoF.vertices\n",
    "        residual.longitud[g.grafoF.vertices+1,j]=0\n",
    "    end\n",
    "    #println(\"residual: \",residual)\n",
    "    #Luego resuelvo Bellman-Ford\n",
    "    conCircuito,longitud,traza=bellmanFord(residual,g.grafoF.vertices+1)\n",
    "    println(\"conCircuito: \",conCircuito,\" \",traza)\n",
    "    #Si hay un circuito\n",
    "    if conCircuito!=0\n",
    "      #detecto circuito\n",
    "      cMin=typemax(Int32)\n",
    "      w=traza[conCircuito]\n",
    "      while w != conCircuito\n",
    "        if residual.longitud[traza[w],w]>0 #arco que añade\n",
    "          c=g.grafoF.capacidad[traza[w],w]-g.grafoF.solucion[traza[w],w]\n",
    "        else # arco que reduce\n",
    "          c=g.grafoF.solucion[w,traza[w]] \n",
    "        end\n",
    "        cMin=min(c,cMin)\n",
    "        #println(w,\" \",traza[w],\" -> \",residual.longitud[traza[w],w],\"  ** \",c)\n",
    "        w=traza[w]\n",
    "      end\n",
    "      if residual.longitud[traza[w],w]>0 # arco que añade\n",
    "        c=g.grafoF.capacidad[traza[w],w]-g.grafoF.solucion[traza[w],w]\n",
    "      else # arco que reduce\n",
    "        c=g.grafoF.solucion[w,traza[w]]\n",
    "      end\n",
    "      cMin=min(c,cMin)\n",
    "      #println(w,\" \",traza[w],\" -> \",residual.longitud[traza[w],w],\" -- \",c)\n",
    "      # alterar el flujo en el circuito\n",
    "      w=traza[conCircuito]\n",
    "      while w != conCircuito\n",
    "        if residual.longitud[traza[w],w]>0\n",
    "          g.grafoF.solucion[traza[w],w] += cMin\n",
    "          assert(g.grafoF.solucion[traza[w],w]<=g.grafoF.capacidad[traza[w],w])\n",
    "          coste += cMin*g.coste[traza[w],w]\n",
    "        else\n",
    "          g.grafoF.solucion[w,traza[w]] -= cMin\n",
    "          assert(g.grafoF.solucion[w,traza[w]]<=g.grafoF.capacidad[w,traza[w]])\n",
    "          coste -= cMin*g.coste[w,traza[w]]\n",
    "        end\n",
    "        w=traza[w]\n",
    "      end\n",
    "      if residual.longitud[traza[w],w]>0\n",
    "        g.grafoF.solucion[traza[w],w] += cMin\n",
    "        assert(g.grafoF.solucion[traza[w],w]<=g.grafoF.capacidad[traza[w],w])\n",
    "        coste += cMin*g.coste[traza[w],w]\n",
    "      else # arco que reduce\n",
    "        g.grafoF.solucion[w,traza[w]] -= cMin\n",
    "        assert(g.grafoF.solucion[w,traza[w]]>=0)\n",
    "        coste -= cMin*g.coste[w,traza[w]]\n",
    "      end\n",
    "      println(\"coste: \",coste)\n",
    "    else\n",
    "      return coste\n",
    "    end\n",
    "  end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "gMinCostEjemplo=grafoMinCost(grafoFlujos(4,[0 3 2 0; 0 0 1 0; 0 0 0 2; 0 0 0 0],zeros(Int64,4,4)),[0 10 50 0; 0 0 30 0; 0 0 0 20; 0 0 0 0])\n",
    "coste=minCostMaxFlow(gMinCostEjemplo,1,4)\n",
    "println(\"coste final: \",coste)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "gMinCost=generarGrafoMinCost(50,50,1000,0.45)\n",
    "coste=minCostMaxFlow(gMinCost,1,50)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 0.6.4",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
